///////////////////////////////////////////////////////////////////////////////
//
// Build Script for building JavaPOS Configuration Loader (JCL) Library
//
// Author: denis.kuniss@dieboldnixdorf.com (2021)
//
///////////////////////////////////////////////////////////////////////////////

plugins {
	id 'java-library'
	id 'signing'
	id 'eclipse'
	id 'maven-publish'
	id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
	id 'project-report'
}


wrapper {
	gradleVersion = '8.12.1'
}


///////////////////////////////////////////////////////////////////////////////
// Names and Versions
///////////////////////////////////////////////////////////////////////////////

def artifactName = 'javapos-config-loader'
group='org.javapos'
version='4.0.2' // if version is going to be changed, first add "-SNAPSHOT" to test publishing to MavenCentral's Snapshot repo


///////////////////////////////////////////////////////////////////////////////
// Build Dependencies
///////////////////////////////////////////////////////////////////////////////

repositories {
	mavenCentral()

    // for resolving snapshots from MavenCentral if not going for a real release
    if (!isReleaseRun()) 
    {
   		logger.warn("WARN: Seems not to be a release, so Maven Central SNAPSHOT repository is fetched!");
	    maven {
	        url 'https://oss.sonatype.org/content/repositories/snapshots'
	    }
    }

    if (!System.getenv('CI')) {
        mavenLocal()
    }
}

// this gets true if Github Actions workflow defines github.event.action project variable at Gradle command line
// AND the event action is 'released' (see release.yml)
def boolean isReleaseRun() {
	return System.getenv('CI') && null != findProperty('github.event.action') && 'released'.equals(getProperty('github.event.action')) 
}

// check whether github.event.action is defined on CI
if (System.getenv('CI') && null == findProperty('github.event.action'))
	logger.warn('WARN: Project property github.event.action is not defined, so, it cannot be determined whether a release is ongoing.')

def testResourceDir = file("${System.properties['java.io.tmpdir']}/javapos-config-loader-test/resources")

dependencies {
	api 'org.javapos:javapos-contracts:1.6.0'
	implementation 'org.slf4j:slf4j-api:2.0.13' 
	
	testImplementation("junit:junit:4.13.2")
	
	testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.13'
	testRuntimeOnly files(testResourceDir)
}

///////////////////////////////////////////////////////////////////////////////
// Project Configurations
///////////////////////////////////////////////////////////////////////////////

def javaposManifest = java.manifest {
        attributes('Specification-Title': 'UnifiedPOS Standard',
				   'Specification-Vendor': 'UnifiedPOS Committee',
                   'Implementation-Title': 'JavaPOS Configuration Loader',
				   'Implementation-Vendor': 'github.com/JavaPOSWorkingGroup',
                   'Implementation-Version': version,
                   'Main-Class': 'jpos.config.Version')
}

///////////////////////////////////////////////////////////////////////////////
// Build Tasks
///////////////////////////////////////////////////////////////////////////////

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
}

jar {
	archiveBaseName = artifactName
	manifest = javaposManifest
	from ('CHANGELOG.md') {
		into 'META-INF'
	}
}

sourcesJar {
	manifest = javaposManifest
    from ('CHANGELOG.md') {
        into 'META-INF'
    }
}


///////////////////////////////////////////////////////////////////////////////
// Test Tasks
///////////////////////////////////////////////////////////////////////////////


task prepareTestConfiguration {
    description = 'copies the initial jpos.properties file to the resource directory where generated jpos.properties files are written to'
    doLast {
		testResourceDir.mkdirs()
		copy {
			from ("$projectDir/src/test/resources/") {
				include "jpos/res/jpos_junit.properties"
			}
			into testResourceDir
			rename 'jpos_junit.properties', 'jpos.properties'
		}
	}
}

test.dependsOn prepareTestConfiguration

// Note: prepareTestConfiguration is needed to be called too before JUnit tests are started in Eclipse


///////////////////////////////////////////////////////////////////////////////
// Artifact Upload
///////////////////////////////////////////////////////////////////////////////

nexusPublishing {
    repositories {
        sonatype()
    }
}

def githubProjectUrl = 'https://github.com/JavaPOSWorkingGroup/javapos-config-loader'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
			pom {
				name = artifactName
				description = 'JavaPOS Configuration Loader Library'
				url = githubProjectUrl
				licenses {
					license {
						name = 'Common Public License (CPL) -- V1.0'
						url = 'https://www.eclipse.org/legal/cpl-v10.html'
					}
				}
				developers {
					developer {
						id = 'javapos'
						name = 'JavaPOS Working Group'
						email = 'builder@javapos.org'
					}
				}
				scm {
                    connection = "scm:${githubProjectUrl}.git"
                    developerConnection = "scm:git:${githubProjectUrl}.git"
                    url = "${githubProjectUrl}.git"
				}
			}
		}
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
